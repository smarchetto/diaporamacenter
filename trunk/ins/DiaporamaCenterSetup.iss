; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "DiaporamaCenter"
#define MyAppExe "DiaporamaCenter.exe"
#define MyAppVersion "1.0.1"

[Setup]
AppID={{6BC287B0-BEC4-42C3-BF56-2303C47F9C34}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName=DiaporamaCenter 1.0.1
AppPublisher=Matheric Tomson (matheric.tomson@gmail.com)
AppPublisherURL=http://code.google.com/p/diaporamacenter/
AppSupportURL=http://code.google.com/p/diaporamacenter/
AppUpdatesURL=http://code.google.com/p/diaporamacenter/
DefaultDirName={code:GetAppDir}
DefaultGroupName={#MyAppName}
OutputDir=..\bin\setup
OutputBaseFilename={#MyAppName}_{#MyAppVersion}_Setup
Compression=lzma/Max
SolidCompression=true
UsePreviousAppDir=false
PrivilegesRequired=lowest
SetupLogging=true

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked

[Types]
Name: full; Description: "Binaries and samples"
Name: binaries; Description: "Binaries only"

[Components]
Name: program; Description: "Program files"; Types: full binaries; Flags: fixed
Name: samples; Description: "Sample files"; Types: full;

[Files]
; program
Source: "..\bin\release\{#MyAppExe}"; DestDir: {code:GetAppDir}; Components: program; Flags: ignoreversion
Source: "rtl120.bpl"; DestDir: {code:GetAppDir}; Components: program; Flags: ignoreversion
Source: "vcl120.bpl"; DestDir: {code:GetAppDir}; Components: program; Flags: ignoreversion
Source: "..\lib\release\CPortLib12.bpl"; DestDir: {code:GetAppDir}; Components: program; Flags: ignoreversion
Source: "..\3rd\msxml.msi"; DestDir: {tmp}; Components: program; Flags: ignoreversion
Source: "..\dat\DiaporamaCenter.xml"; DestDir: {code:GetAppDir}; Components: program;  Flags: ignoreversion

;samples
Source: "..\dat\FantasticEstateAgency\*"; DestDir: {code:GetAppDir}; Components: samples; Flags: ignoreversion recursesubdirs

; NOTE: Don't use Flags: ignoreversion on any shared system files

[Icons]
Name: {group}\{#MyAppName}; Filename: {code:GetAppDir}
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}
Name: {commondesktop}\{#MyAppName}; Filename: {code:GetAppDir}\{#MyAppExe}; Tasks: desktopicon

[Run]
Filename: "msiexec.exe"; Parameters: "/i {tmp}\msxml.msi"
Filename: {code:GetAppDir}\{#MyAppExe}; Description: {cm:LaunchProgram,{#MyAppName}}; Flags: nowait postinstall skipifsilent 

[Code]

function IsStandardUser(): Boolean;
begin
  Result := not (IsAdminLoggedOn or IsPowerUserLoggedOn);
end;

function GetAppDir(param: string): string;
begin
  if IsStandardUser then
    Result := ExpandConstant('{localappdata}')
  else
    Result := ExpandConstant('{pf}')
  Result := Result + '\' + '{#MyAppName}';
end;


